plugins {
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'java'
    id 'idea'
}

group 'com.winllc.ra.est'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()

    maven {
        url "http://util.winllc-dev.com:8081/repository/win-llc/"
        allowInsecureProtocol true
    }

    maven {
        url "http://util.winllc-dev.com:8081/repository/win-llc-releases/"
        allowInsecureProtocol true
    }
}

configurations.all {
    resolutionStrategy {
        preferProjectModules()
        cacheChangingModulesFor 0, 'seconds'

        dependencySubstitution {
            // Substitute project and module dependencies

            def proj1 = findProject(":pki-ra-common")
            if (proj1 != null && proj1.buildFile.exists()) {
                substitute module('com.winllc.acme.common:pki-ra-common') with project(':pki-ra-common')
            }

        }
    }
}

bootJar {
    enabled(true)
}

dependencies {
    implementation group: 'com.winllc.acme.common', name: 'pki-ra-common', version: '1.0-SNAPSHOT'

    implementation('org.springframework.boot:spring-boot-starter-actuator')
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-security')
    implementation('org.springframework.boot:spring-boot-autoconfigure')

    // https://mvnrepository.com/artifact/org.bouncycastle/bcprov-jdk15on
    implementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.70'
    implementation group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.70'

    // https://mvnrepository.com/artifact/javax.inject/javax.inject
    implementation group: 'javax.inject', name: 'javax.inject', version: '1'


    runtimeOnly('org.springframework.boot:spring-boot-devtools')
    annotationProcessor group: 'org.springframework.boot', name: 'spring-boot-configuration-processor'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
}